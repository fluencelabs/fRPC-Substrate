import "@fluencelabs/aqua-lib/builtin.aqua"
import Subnet, Worker from "@fluencelabs/aqua-lib/subnet.aqua"
import "services.aqua"
use "deals.aqua"

export randomLoadBalancingEth, roundRobinEth, quorumEth, Counter, Logger

data QuorumResult:
  value: string
  results: []JsonString
  error: string

service Logger("logger"):
  log(s: []string)
  logNum(n: u32)
  logCall(s: string)
  logWorker(w: Worker)

service NumOp("op"):
  identity(n: u64) -> i64

service Counter("counter"):
  incrementAndReturn() -> u32

service QuorumChecker("quorum"):
  check(results: []JsonString, minResults: u32) -> QuorumResult

func empty() -> JsonString:
  <- JsonString(value = "", success = true, error = "")

func call(uri: string, method: string, jsonArgs: []string) -> JsonString:
  res <- EthRpc.eth_call(uri, method, jsonArgs)
  <- res

func getWorkers() -> []Worker, ?string:
  deals <- Deals.get()
  dealId = deals.defaultWorker!.dealIdOriginal
  on HOST_PEER_ID:
    result <- Subnet.resolve(dealId)
    workers = result.workers
    error = result.error
  <- workers, error

func callOnWorker(worker: Worker, uri: string, method: string, jsonArgs: []string, callFunc: string, string, []string -> JsonString) -> JsonString:
  on worker.worker_id! via worker.host_id:
    result <- callFunc(uri, method, jsonArgs)
  <- result

func randomLoadBalancing(uris: []string, method: string, jsonArgs: []string, callFunc: string, string, []string -> JsonString) -> JsonString:
  on HOST_PEER_ID:
    workers <- getWorkers()
    workersNum = workers.length
    -- choose worker randomly
    timeW <- NumOp.identity(Peer.timestamp_sec())
    workerNumber = timeW % workers.length
    worker = workers[workerNumber]
    -- choose provider randomly
    timeP <- NumOp.identity(Peer.timestamp_sec())
    providerNumber = timeP % uris.length
    provider = uris[providerNumber]
    result <- callOnWorker(worker, provider, method, jsonArgs, callFunc)
  Logger.logWorker(worker)
  Logger.logCall(uris[providerNumber])
  <- result

func randomLoadBalancingEth(uris: []string, method: string, jsonArgs: []string) -> JsonString:
  <- randomLoadBalancing(uris, method, jsonArgs, call)

func roundRobin(uris: []string, method: string, jsonArgs: []string, counterServiceId: string, counterPeerId: string, callFunc: string, string, []string -> JsonString) -> JsonString:
  on counterPeerId:
    Counter counterServiceId
    requestNumber <- Counter.incrementAndReturn()
  on HOST_PEER_ID:
    workers, error <- getWorkers()
    result: *JsonString
    if error == nil:
      workerNumber = requestNumber % workers.length
      worker = workers[workerNumber]
      providerNumber = requestNumber % uris.length
      provider = uris[providerNumber]
      result <<- callOnWorker(worker, provider, method, jsonArgs, callFunc)
      Op.noop() -- dirty hack
      on INIT_PEER_ID:
        Logger.logWorker(worker)
        Logger.logCall(uris[providerNumber])
    else:
      result <<- JsonString(value = "", success = false, error = error!)

  <- result!

func roundRobinEth(uris: []string, method: string, jsonArgs: []string, counterServiceId: string, counterPeerId: string) -> JsonString:
  <- roundRobin(uris, method, jsonArgs, counterServiceId, counterPeerId, call)

func quorum(
  uris: []string, quorumNumber: u32, timeout: u32, method: string, jsonArgs: []string, quorumServiceId: string, quorumPeerId: string,
  callFunc: string, string, []string -> JsonString
) -> QuorumResult:
  results: *JsonString
  on HOST_PEER_ID:
    workers <- getWorkers()
    for worker <- workers par:
      on worker.worker_id! via worker.host_id:
        -- choose provider randomly
        timeP <- NumOp.identity(Peer.timestamp_ms())
        providerNumber = timeP % uris.length
        provider = uris[providerNumber]
        results <- callFunc(provider, method, jsonArgs)
    -- wait all results from all workers with timeout
    join results[workers.length - 1]
    par Peer.timeout(timeout, "")
  on quorumPeerId via HOST_PEER_ID:
    Counter quorumServiceId
    -- check all results that we got
    quorumResult <- QuorumChecker.check(results, quorumNumber)
  <- quorumResult

func quorumEth(uris: []string, quorumNumber: u32, timeout: u32, method: string, jsonArgs: []string, quorumServiceId: string, quorumPeerId: string) -> QuorumResult:
  <- quorum(uris, quorumNumber, timeout, method, jsonArgs, quorumServiceId, quorumPeerId, call)